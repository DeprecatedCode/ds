L: @console.log

'--------------------------------'                               L
'DefaultScript Test Suite: Arrays'                               L
'--------------------------------'                               L

cart: ['apple', 'banana', 'carrot', 'apple', 'beet']

'1: There are ' cart.length ' items in the cart.'                L

isFruit: {@it ['apple', 'banana', 'orange'].indexOf != -1}

'2: Is an apple a fruit? ' ('apple' isFruit)                     L

truckTalk: {&, '3: ' (0 colors) ' fire trucks are everywhere.'}

{colors: ['Red', 'Blue']} truckTalk                              L

exclude: {
  filter: @it.filter
  {condition: @it, {@it condition !} filter}
}

fruitOnly: isFruit cart.filter
nonFruit: isFruit (cart exclude)

'4: The cart has ' fruitOnly.length ' fruits and '     \
                nonFruit.length  ' other items in it.'           L

'5: The ' (1 ['first', 'second'])            \
  ' item in the cart is a delicious yellow ' \
  (1 cart) '.'                                                   L

'6: The cart has a type of: ' (cart @type)                       L

biggerCart: ['lemon', & cart, 'pineapple']

'7: Create a bigger cart with the old cart in the middle: ' (
  ' - ' biggerCart.join '!'
)                                                                L
